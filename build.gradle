// releng: gradle clean && gradle uploadArchives -Prelease

group 'net.moznion'
version '1.0.0'

apply plugin: 'java'
apply plugin: "maven"
apply plugin: "signing"
apply plugin: 'war'

sourceCompatibility = 1.8

def isDevBuild
def isCiBuild
def isReleaseBuild
def sonatypeRepositoryUrl
if (hasProperty("release")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else if (hasProperty("ci")) {
    isCiBuild = true
    version += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    isDevBuild = true
    version += "-SNAPSHOT"
}

repositories {
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from "${buildDir}/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required { isReleaseBuild }
    sign configurations.archives
}

uploadArchives {
    repositories {
        if (isDevBuild) {
            mavenLocal()
        } else {
            mavenDeployer {
                if (isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'euphoriq'
                    packaging 'jar'
                    description 'Simple job queue framework'
                    url 'https://github.com/moznion/euphoriq'

                    scm {
                        url "scm:git@github.com:moznion/euphoriq.git"
                        connection "scm:git@github.com:moznion/euphoriq.git"
                        developerConnection "scm:git@github.com:moznion/euphoriq.git"
                    }
                    licenses {
                        license {
                            name 'MIT'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id 'moznion'
                            name 'Taiki Kawakami'
                            email 'moznion@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    providedCompile group: 'org.projectlombok', name: 'lombok', version: '1.16.8'

    compile group: 'redis.clients', name: 'jedis', version: '2.8.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.7.3'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.19'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.19'

    runtime group: 'mysql', name: 'mysql-connector-java', version: '5.1.38'
    compile group: 'com.zaxxer', name: 'HikariCP', version: '2.4.5'
    compile group: 'me.geso', name: 'tinyorm', version: '1.10.0'

    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.4.1'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

